@Глобально
метод Тест(Имя: Строка, Содержимое: () -> ничто)
    если Имя.Пусто()
        выбросить новый ИсключениеВалидации("Имя теста не должно быть пустым")
    ;
    Тесты.Добавить(новый ЭлементНабораТестов(Имя, Содержимое))
;

@Глобально
метод ПередКаждым(Содержимое: () -> ничто)
    ПередКаждым = новый ЭлементНабораТестов("", Содержимое)
;

@Глобально
метод ПослеКаждого(Содержимое: () -> ничто)
    ПослеКаждого = новый ЭлементНабораТестов("", Содержимое)
;

@ВПодсистеме
метод Запустить()
    для Тест из Тесты
        пер Сообщение: Строка
        попытка
            ВызватьОбработчикСобытия(ПередКаждым)
            Тест.Содержимое()
            ВызватьОбработчикСобытия(ПослеКаждого)
            Сообщение = "✅ %{Тест.Имя}"
        поймать Исключение: неизвестно
            Сообщение = "❌ %{Тест.Имя} - %{Исключение.ВСтроку()}"
        ;
        Результаты.Добавить(Сообщение)
    ;
;

метод ВызватьОбработчикСобытия(Событие: ЭлементНабораТестов?)
    если Событие != Неопределено
        Событие.Содержимое()
    ;
;